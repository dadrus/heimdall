grafana_version := '2.7.15'
prometheus_version := '44.2.1'
loki_version := '2.8.9'
tempo_version := '0.16.8'
phlare_version := '0.1.2'
nginx_version := '9.7.7'
contour_version := '12.2.4'
metallb_version := '0.13.10'
certmanager_version := '1.12.3'

kind_base_domain := '127.0.0.1.nip.io'
cluster_name := 'demo-cluster'

setup-charts:
  helm repo add bitnami https://charts.bitnami.com/bitnami
  helm repo add prometheus-community https://prometheus-community.github.io/helm-charts
  helm repo add grafana https://grafana.github.io/helm-charts
  helm repo add metallb https://metallb.github.io/metallb
  helm repo add jetstack https://charts.jetstack.io
  helm repo update

## Installs Grafana
install-grafana base_host=kind_base_domain: setup-charts
  #!/usr/bin/env bash
  helm upgrade --install grafana bitnami/grafana-operator \
    -n monitoring --create-namespace \
    --set grafana.ingress.hostname="grafana.{{base_host}}" \
    --set grafana.config.server.root_url="https://grafana.{{base_host}}" \
    --values grafana/helm-values.yaml \
    --version {{grafana_version}} \
    --wait
  while : ; do
    kubectl rollout -n monitoring status deployment grafana-deployment && break
    sleep 2
  done


# Installs Prometheus
install-prometheus: setup-charts
  helm upgrade --install prometheus prometheus-community/kube-prometheus-stack \
    -n monitoring --create-namespace \
    --set grafana.enabled=false \
    --version {{prometheus_version}} \
    --wait
  kubectl apply -f grafana/data-sources/prometheus.yaml

# Installs Loki
install-loki: setup-charts
  helm upgrade --install loki grafana/loki-stack \
    -n monitoring --create-namespace \
    --version {{loki_version}} \
    --set grafana.enabled=false \
    --wait
  kubectl apply -f grafana/data-sources/loki.yaml

# Installs Tempo
install-tempo: setup-charts
  helm upgrade --install tempo grafana/tempo \
    -n monitoring --create-namespace \
    --set tempo.searchEnabled=true \
    --version {{tempo_version}} \
    --wait
  kubectl apply -f grafana/data-sources/tempo.yaml

# Installs Phlareheimdall
install-phlare: setup-charts
  helm upgrade --install phlare grafana/phlare \
   -n monitoring --create-namespace \
   --version {{phlare_version}} \
   --wait
  kubectl apply -f grafana/data-sources/phlare.yaml

install-dashboards:
  #!/usr/bin/env bash
  for f in grafana/dashboards/*.yaml ; do
    kubectl apply -f $f
  done

install-observability-stack: install-grafana install-prometheus install-loki install-tempo install-phlare install-dashboards

install-nginx-ingress-controller:
  kubectl apply -f https://raw.githubusercontent.com/kubernetes/ingress-nginx/main/deploy/static/provider/kind/deploy.yaml --wait=true
  #helm upgrade --install \
  #  nginx-ic bitnami/nginx-ingress-controller \
  #  -n nginx-ic \
  #  --create-namespace \
  #  --version {{nginx_version}} \
  #  -f nginx-values.yaml \
  #  --wait

install-contour-ingress-controller: setup-charts
  helm upgrade --install contour-ingress-controller bitnami/contour \
    -n projectcontour --create-namespace \
    --set contour.debug=true \
    --version {{contour_version}} \
    --wait

install-ingress-controller: install-nginx-ingress-controller install-contour-ingress-controller

install-lb: setup-charts
  #!/usr/bin/env bash
  helm upgrade --install metallb metallb/metallb \
    -n metallb-system --create-namespace \
    --version {{metallb_version}} \
    --wait

  ./configure-metallb.sh

install-cert-manager: setup-charts
  #!/usr/bin/env bash
  kubectl apply -f https://github.com/cert-manager/cert-manager/releases/download/v{{certmanager_version}}/cert-manager.crds.yaml

  helm upgrade --install cert-manager jetstack/cert-manager \
    -n cert-manager --create-namespace \
    --version {{certmanager_version}} \
    --set installCRDs=false \
    --set featureGates='AdditionalCertificateOutputFormats=true' \
    --wait

  kubectl apply -n cert-manager -f - <<EOF
  apiVersion: cert-manager.io/v1
  kind: ClusterIssuer
  metadata:
    name: selfsigned
  spec:
    selfSigned: {}
  EOF

install-echo-service:
  kustomize build quickstarts/overlays/contour-ingress | kubectl apply -f -

install-heimdall:
  kubectl create namespace heimdall
  kubectl apply -f quickstarts/heimdall-certificate.yaml
  helm upgrade --install heimdall -n heimdall \
    -f quickstarts/heimdall-config.yaml \
    -f quickstarts/heimdall-values.yaml \
    --wait ../../charts/heimdall
  kubectl apply -f quickstarts/heimdall-extension-service.yaml

create-cluster:
  kind create cluster --config kind/kind.yaml --name {{cluster_name}}

setup-cluster: create-cluster install-lb install-cert-manager install-contour-ingress-controller install-heimdall install-echo-service

delete-cluster:
  kind delete clusters {{cluster_name}}

