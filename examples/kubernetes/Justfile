grafana_version := '2.7.15'
prometheus_version := '44.2.1'
loki_version := '2.8.9'
tempo_version := '0.16.8'
phlare_version := '0.1.2'
nginx_version := '9.7.7'
contour_version := '12.2.4'
metallb_version := '0.13.10'
certmanager_version := '1.12.3'

kind_base_domain := '127.0.0.1.nip.io'
cluster_name := 'demo-cluster'

setup-charts:
  helm repo add bitnami https://charts.bitnami.com/bitnami
  helm repo add prometheus-community https://prometheus-community.github.io/helm-charts
  helm repo add grafana https://grafana.github.io/helm-charts
  helm repo add metallb https://metallb.github.io/metallb
  helm repo add jetstack https://charts.jetstack.io
  helm repo update

## Installs Grafana
install-grafana base_host=kind_base_domain: setup-charts
  #!/usr/bin/env bash
  helm upgrade --install grafana bitnami/grafana-operator \
    -n monitoring --create-namespace \
    --set grafana.ingress.hostname="grafana.{{base_host}}" \
    --set grafana.config.server.root_url="https://grafana.{{base_host}}" \
    --values grafana/helm-values.yaml \
    --version {{grafana_version}} \
    --wait
  while : ; do
    kubectl rollout -n monitoring status deployment grafana-deployment && break
    sleep 2
  done


# Installs Prometheus
install-prometheus: setup-charts
  helm upgrade --install prometheus prometheus-community/kube-prometheus-stack \
    -n monitoring --create-namespace \
    --set grafana.enabled=false \
    --version {{prometheus_version}} \
    --wait
  kubectl apply -f grafana/data-sources/prometheus.yaml

# Installs Loki
install-loki: setup-charts
  helm upgrade --install loki grafana/loki-stack \
    -n monitoring --create-namespace \
    --version {{loki_version}} \
    --set grafana.enabled=false \
    --wait
  kubectl apply -f grafana/data-sources/loki.yaml

# Installs Tempo
install-tempo: setup-charts
  helm upgrade --install tempo grafana/tempo \
    -n monitoring --create-namespace \
    --set tempo.searchEnabled=true \
    --version {{tempo_version}} \
    --wait
  kubectl apply -f grafana/data-sources/tempo.yaml

# Installs Phlareheimdall
install-phlare: setup-charts
  helm upgrade --install phlare grafana/phlare \
   -n monitoring --create-namespace \
   --version {{phlare_version}} \
   --wait
  kubectl apply -f grafana/data-sources/phlare.yaml

install-dashboards:
  #!/usr/bin/env bash
  for f in grafana/dashboards/*.yaml ; do
    kubectl apply -f $f
  done

install-observability-stack: install-grafana install-prometheus install-loki install-tempo install-phlare install-dashboards

install-nginx-ingress-controller:
  kubectl apply -f https://raw.githubusercontent.com/kubernetes/ingress-nginx/main/deploy/static/provider/kind/deploy.yaml --wait=true
  #helm upgrade --install \
  #  nginx-ic bitnami/nginx-ingress-controller \
  #  -n nginx-ic \
  #  --create-namespace \
  #  --version {{nginx_version}} \
  #  -f nginx-values.yaml \
  #  --wait

install-contour-ingress-controller: setup-charts
  #kubectl apply -f https://projectcontour.io/quickstart/contour.yaml --wait=true
  #kubectl patch daemonsets -n projectcontour envoy -p '{"spec":{"template":{"spec":{"nodeSelector":{"ingress-ready":"true"},"tolerations":[{"key":"node-role.kubernetes.io/control-plane","operator":"Equal","effect":"NoSchedule"},{"key":"node-role.kubernetes.io/master","operator":"Equal","effect":"NoSchedule"}]}}}}'
  helm upgrade --install contour-ingress-controller bitnami/contour \
    -n projectcontour --create-namespace \
    --set contour.debug=true \
    --version {{contour_version}} \
    --wait

install-ingress-controller: install-nginx-ingress-controller install-contour-ingress-controller

install-lb: setup-charts
  #!/usr/bin/env bash
  helm upgrade --install metallb metallb/metallb \
    -n metallb-system --create-namespace \
    --version {{metallb_version}} \
    --wait

  KIND_SUBNET=$(docker network inspect kind -f '{{(index .IPAM.Config 0).Subnet}}')
  METALLB_IP_START=$(echo ${KIND_SUBNET} | sed "s@0.0/16@255.200@")
  METALLB_IP_END=$(echo ${KIND_SUBNET} | sed "s@0.0/16@255.250@")
  METALLB_IP_RANGE="${METALLB_IP_START}-${METALLB_IP_END}"

  kubectl apply -f - <<EOF
  apiVersion: metallb.io/v1beta1
  kind: IPAddressPool
  metadata:
    name: example
    namespace: metallb-system
  spec:
    addresses:
    - ${METALLB_IP_RANGE}
  ---
  apiVersion: metallb.io/v1beta1
  kind: L2Advertisement
  metadata:
    name: empty
    namespace: metallb-system
  EOF

install-cert-manager: setup-charts
  #!/usr/bin/env bash
  kubectl apply -f https://github.com/cert-manager/cert-manager/releases/download/v{{certmanager_version}}/cert-manager.crds.yaml

  helm upgrade --install cert-manager jetstack/cert-manager \
    -n cert-manager --create-namespace \
    --version {{certmanager_version}} \
    --set installCRDs=false \
    --set featureGates='AdditionalCertificateOutputFormats=true' \
    --wait

  kubectl apply -n cert-manager -f - <<EOF
  apiVersion: cert-manager.io/v1
  kind: ClusterIssuer
  metadata:
    name: selfsigned
  spec:
    selfSigned: {}
  EOF

create-cluster:
  kind create cluster --config kind/kind.yaml --name {{cluster_name}}

setup-cluster: create-cluster install-ingress-controller install-observability-stack

delete-cluster:
  kind delete clusters {{cluster_name}}

